(define (make-interval a b) (cons a b))
(define (lower-bound x) (car x))
(define (upper-bound x) (cdr x))
(define (sub-interval a b)
  (make-interval (- (lower-bound a) (upper-bound b))
                 (- (upper-bound a) (lower-bound b))))
(define (add-interval x y)
  (make-interval (+ (lower-bound x) (lower-bound y))
                 (+ (upper-bound x) (upper-bound y))))
(define (div-interval x y)
  (if (or (and (positive? (upper-bound x)) (negative? (lower-bound x)))
          (and (positive? (upper-bound y)) (negative? (lower-bound y))))
    (error "Intervals must not span zero" x y)
    (mul-interval x
                  (make-interval (/ 1.0 (upper-bound y))
                                 (/ 1.0 (lower-bound y))))))
(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define (mul-interval x y)
  (cond ((and (positive? (lower-bound x)) (positive? (lower-bound y)))
         (make-interval (* (lower-bound x) (lower-bound y))
                        (* (upper-bound x) (upper-bound y))))
        ((and (negative? (upper-bound x)) (negative? (upper-bound y)))
         (make-interval (* (upper-bound x) (upper-bound y))
                        (* (lower-bound x) (lower-bound y))))
        ((and (positive? (lower-bound x)) (negative? (upper-bound y)))
         (make-interval (* (upper-bound x) (lower-bound y))
                        (* (lower-bound x) (upper-bound y))))
        ((and (negative? (upper-bound x)) (positive? (lower-bound y)))
         (make-interval (* (lower-bound x) (upper-bound y))
                        (* (upper-bound x) (lower-bound y))))
        ((positive? (lower-bound x))
         (make-interval (* (upper-bound x) (lower-bound y))
                        (* (upper-bound x) (upper-bound y))))
        ((positive? (lower-bound y))
         (make-interval (* (lower-bound x) (upper-bound y))
                        (* (upper-bound x) (upper-bound y))))
        ((negative? (upper-bound x))
         (make-interval (* (lower-bound x) (upper-bound y))
                        (* (lower-bound x) (lower-bound y))))
        ((negative? (upper-bound y))
         (make-interval (* (upper-bound x) (lower-bound y))
                        (* (lower-bound x) (lower-bound y))))
        (else (let ((p1 (* (lower-bound x) (lower-bound y)))
                    (p2 (* (lower-bound x) (upper-bound y)))
                    (p3 (* (upper-bound x) (lower-bound y)))
                    (p4 (* (upper-bound x) (upper-bound y))))
                (make-interval (min p1 p2 p3 p4)
                               (max p1 p2 p3 p4))))))

; test

(mul-interval (make-interval 10 20)
              (make-interval 10 11))

(mul-interval (make-interval -20 -10)
              (make-interval -11 -10))

(mul-interval (make-interval 3 4)
              (make-interval -6 -5))

(mul-interval (make-interval -4 -3)
              (make-interval 5 6))

(mul-interval (make-interval 4 9)
              (make-interval -5 6))

(mul-interval (make-interval -4 4)
              (make-interval 5 6))

(mul-interval (make-interval -9 -4)
              (make-interval -5 6))

(mul-interval (make-interval -4 4)
              (make-interval -10 -6))

(mul-interval (make-interval -3 4)
              (make-interval -6 7))

(mul-interval (make-interval 0 10)
              (make-interval -3 15))

(mul-interval (make-interval 0 10)
              (make-interval 3 15))
